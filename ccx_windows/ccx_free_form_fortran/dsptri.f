      SUBROUTINE DSPTRI( UPLO, N, AP, IPIV, WORK, INFO )
      !
      !   -- LAPACK routine (version 2.0) --
      !      Univ. of Tennessee, Univ. of California Berkeley, NAG Ltd.,
      !      Courant Institute, Argonne National Lab, and Rice University
      !      March 31, 1993
      !
      !      .. Scalar Arguments ..
      CHARACTER          UPLO
      INTEGER            INFO, N
      !      ..
      !      .. Array Arguments ..
      INTEGER            IPIV( * )
      DOUBLE PRECISION   AP( * ), WORK( * )
      !      ..
      !
      !   Purpose
      !   =======
      !
      !   DSPTRI computes the inverse of a real symmetric indefinite matrix
      !   A in packed storage using the factorization A = U*D*U**T or
      !   A = L*D*L**T computed by DSPTRF.
      !
      !   Arguments
      !   =========
      !
      !   UPLO    (input) CHARACTER*1
      !           Specifies whether the details of the factorization are stored
      !           as an upper or lower triangular matrix.
      !           = 'U':  Upper triangular, form is A = U*D*U**T;
      !           = 'L':  Lower triangular, form is A = L*D*L**T.
      !
      !   N       (input) INTEGER
      !           The order of the matrix A.  N >= 0.
      !
      !   AP      (input/output) DOUBLE PRECISION array, dimension (N*(N+1)/2)
      !           On entry, the block diagonal matrix D and the multipliers
      !           used to obtain the factor U or L as computed by DSPTRF,
      !           stored as a packed triangular matrix.
      !
      !           On exit, if INFO = 0, the (symmetric) inverse of the original
      !           matrix, stored as a packed triangular matrix. The j-th column
      !           of inv(A) is stored in the array AP as follows:
      !           if UPLO = 'U', AP(i + (j-1)*j/2) = inv(A)(i,j) for 1<=i<=j;
      !           if UPLO = 'L',
      !              AP(i + (j-1)*(2n-j)/2) = inv(A)(i,j) for j<=i<=n.
      !
      !   IPIV    (input) INTEGER array, dimension (N)
      !           Details of the interchanges and the block structure of D
      !           as determined by DSPTRF.
      !
      !   WORK    (workspace) DOUBLE PRECISION array, dimension (N)
      !
      !   INFO    (output) INTEGER
      !           = 0: successful exit
      !           < 0: if INFO = -i, the i-th argument had an illegal value
      !           > 0: if INFO = i, D(i,i) = 0; the matrix is singular and its
      !                inverse could not be computed.
      !
      !   =====================================================================
      !
      !      .. Parameters ..
      DOUBLE PRECISION   ONE, ZERO
      PARAMETER          ( ONE = 1.0D+0, ZERO = 0.0D+0 )
      !      ..
      !      .. Local Scalars ..
      LOGICAL            UPPER
      INTEGER            J, K, KC, KCNEXT, KP, KPC, KSTEP, KX, NPP
      DOUBLE PRECISION   AK, AKKP1, AKP1, D, T, TEMP
      !      ..
      !      .. External Functions ..
      LOGICAL            LSAME
      DOUBLE PRECISION   DDOT
      EXTERNAL           LSAME, DDOT
      !      ..
      !      .. External Subroutines ..
      EXTERNAL           DCOPY, DSPMV, DSWAP, XERBLA
      !      ..
      !      .. Intrinsic Functions ..
      INTRINSIC          ABS
      !      ..
      !      .. Executable Statements ..
      !
      !      Test the input parameters.
      !
      INFO = 0
      UPPER = LSAME( UPLO, 'U' )
      IF( .NOT.UPPER .AND. .NOT.LSAME( UPLO, 'L' ) ) THEN
         INFO = -1
      ELSE IF( N.LT.0 ) THEN
         INFO = -2
      END IF
      IF( INFO.NE.0 ) THEN
         CALL XERBLA( 'DSPTRI', -INFO )
         RETURN
      END IF
      !
      !      Quick return if possible
      !
      IF( N.EQ.0 )&
         RETURN
      !
      !      Check that the diagonal matrix D is nonsingular.
      !
      IF( UPPER ) THEN
         !
         !         Upper triangular storage: examine D from bottom to top
         !
         KP = N*( N+1 ) / 2
         DO 10 INFO = N, 1, -1
            IF( IPIV( INFO ).GT.0 .AND. AP( KP ).EQ.ZERO )&
               RETURN
            KP = KP - INFO
   10    CONTINUE
      ELSE
         !
         !         Lower triangular storage: examine D from top to bottom.
         !
         KP = 1
         DO 20 INFO = 1, N
            IF( IPIV( INFO ).GT.0 .AND. AP( KP ).EQ.ZERO )&
               RETURN
            KP = KP + N - INFO + 1
   20    CONTINUE
      END IF
      INFO = 0
      !
      IF( UPPER ) THEN
         !
         !         Compute inv(A) from the factorization A = U*D*U'.
         !
         !         K is the main loop index, increasing from 1 to N in steps of
         !         1 or 2, depending on the size of the diagonal blocks.
         !
         K = 1
         KC = 1
   30    CONTINUE
         !
         !         If K > N, exit from loop.
         !
         IF( K.GT.N )&
            GO TO 50
         !
         KCNEXT = KC + K
         IF( IPIV( K ).GT.0 ) THEN
            !
            !            1 x 1 diagonal block
            !
            !            Invert the diagonal block.
            !
            AP( KC+K-1 ) = ONE / AP( KC+K-1 )
            !
            !            Compute column K of the inverse.
            !
            IF( K.GT.1 ) THEN
               CALL DCOPY( K-1, AP( KC ), 1, WORK, 1 )
               CALL DSPMV( UPLO, K-1, -ONE, AP, WORK, 1, ZERO, AP( KC ),&
                           1 )
               AP( KC+K-1 ) = AP( KC+K-1 ) -&
                              DDOT( K-1, WORK, 1, AP( KC ), 1 )
            END IF
            KSTEP = 1
         ELSE
            !
            !            2 x 2 diagonal block
            !
            !            Invert the diagonal block.
            !
            T = ABS( AP( KCNEXT+K-1 ) )
            AK = AP( KC+K-1 ) / T
            AKP1 = AP( KCNEXT+K ) / T
            AKKP1 = AP( KCNEXT+K-1 ) / T
            D = T*( AK*AKP1-ONE )
            AP( KC+K-1 ) = AKP1 / D
            AP( KCNEXT+K ) = AK / D
            AP( KCNEXT+K-1 ) = -AKKP1 / D
            !
            !            Compute columns K and K+1 of the inverse.
            !
            IF( K.GT.1 ) THEN
               CALL DCOPY( K-1, AP( KC ), 1, WORK, 1 )
               CALL DSPMV( UPLO, K-1, -ONE, AP, WORK, 1, ZERO, AP( KC ),&
                           1 )
               AP( KC+K-1 ) = AP( KC+K-1 ) -&
                              DDOT( K-1, WORK, 1, AP( KC ), 1 )
               AP( KCNEXT+K-1 ) = AP( KCNEXT+K-1 ) -&
                                  DDOT( K-1, AP( KC ), 1, AP( KCNEXT ),&
                                  1 )
               CALL DCOPY( K-1, AP( KCNEXT ), 1, WORK, 1 )
               CALL DSPMV( UPLO, K-1, -ONE, AP, WORK, 1, ZERO,&
                           AP( KCNEXT ), 1 )
               AP( KCNEXT+K ) = AP( KCNEXT+K ) -&
                                DDOT( K-1, WORK, 1, AP( KCNEXT ), 1 )
            END IF
            KSTEP = 2
            KCNEXT = KCNEXT + K + 1
         END IF
         !
         KP = ABS( IPIV( K ) )
         IF( KP.NE.K ) THEN
            !
            !            Interchange rows and columns K and KP in the leading
            !            submatrix A(1:k+1,1:k+1)
            !
            KPC = ( KP-1 )*KP / 2 + 1
            CALL DSWAP( KP-1, AP( KC ), 1, AP( KPC ), 1 )
            KX = KPC + KP - 1
            DO 40 J = KP + 1, K - 1
               KX = KX + J - 1
               TEMP = AP( KC+J-1 )
               AP( KC+J-1 ) = AP( KX )
               AP( KX ) = TEMP
   40       CONTINUE
            TEMP = AP( KC+K-1 )
            AP( KC+K-1 ) = AP( KPC+KP-1 )
            AP( KPC+KP-1 ) = TEMP
            IF( KSTEP.EQ.2 ) THEN
               TEMP = AP( KC+K+K-1 )
               AP( KC+K+K-1 ) = AP( KC+K+KP-1 )
               AP( KC+K+KP-1 ) = TEMP
            END IF
         END IF
         !
         K = K + KSTEP
         KC = KCNEXT
         GO TO 30
   50    CONTINUE
      !
      ELSE
         !
         !         Compute inv(A) from the factorization A = L*D*L'.
         !
         !         K is the main loop index, increasing from 1 to N in steps of
         !         1 or 2, depending on the size of the diagonal blocks.
         !
         NPP = N*( N+1 ) / 2
         K = N
         KC = NPP
   60    CONTINUE
         !
         !         If K < 1, exit from loop.
         !
         IF( K.LT.1 )&
            GO TO 80
         !
         KCNEXT = KC - ( N-K+2 )
         IF( IPIV( K ).GT.0 ) THEN
            !
            !            1 x 1 diagonal block
            !
            !            Invert the diagonal block.
            !
            AP( KC ) = ONE / AP( KC )
            !
            !            Compute column K of the inverse.
            !
            IF( K.LT.N ) THEN
               CALL DCOPY( N-K, AP( KC+1 ), 1, WORK, 1 )
               CALL DSPMV( UPLO, N-K, -ONE, AP( KC+N-K+1 ), WORK, 1,&
                           ZERO, AP( KC+1 ), 1 )
               AP( KC ) = AP( KC ) - DDOT( N-K, WORK, 1, AP( KC+1 ), 1 )
            END IF
            KSTEP = 1
         ELSE
            !
            !            2 x 2 diagonal block
            !
            !            Invert the diagonal block.
            !
            T = ABS( AP( KCNEXT+1 ) )
            AK = AP( KCNEXT ) / T
            AKP1 = AP( KC ) / T
            AKKP1 = AP( KCNEXT+1 ) / T
            D = T*( AK*AKP1-ONE )
            AP( KCNEXT ) = AKP1 / D
            AP( KC ) = AK / D
            AP( KCNEXT+1 ) = -AKKP1 / D
            !
            !            Compute columns K-1 and K of the inverse.
            !
            IF( K.LT.N ) THEN
               CALL DCOPY( N-K, AP( KC+1 ), 1, WORK, 1 )
               CALL DSPMV( UPLO, N-K, -ONE, AP( KC+( N-K+1 ) ), WORK, 1,&
                           ZERO, AP( KC+1 ), 1 )
               AP( KC ) = AP( KC ) - DDOT( N-K, WORK, 1, AP( KC+1 ), 1 )
               AP( KCNEXT+1 ) = AP( KCNEXT+1 ) -&
                                DDOT( N-K, AP( KC+1 ), 1,&
                                AP( KCNEXT+2 ), 1 )
               CALL DCOPY( N-K, AP( KCNEXT+2 ), 1, WORK, 1 )
               CALL DSPMV( UPLO, N-K, -ONE, AP( KC+( N-K+1 ) ), WORK, 1,&
                           ZERO, AP( KCNEXT+2 ), 1 )
               AP( KCNEXT ) = AP( KCNEXT ) -&
                              DDOT( N-K, WORK, 1, AP( KCNEXT+2 ), 1 )
            END IF
            KSTEP = 2
            KCNEXT = KCNEXT - ( N-K+3 )
         END IF
         !
         KP = ABS( IPIV( K ) )
         IF( KP.NE.K ) THEN
            !
            !            Interchange rows and columns K and KP in the trailing
            !            submatrix A(k-1:n,k-1:n)
            !
            KPC = NPP - ( N-KP+1 )*( N-KP+2 ) / 2 + 1
            IF( KP.LT.N )&
               CALL DSWAP( N-KP, AP( KC+KP-K+1 ), 1, AP( KPC+1 ), 1 )
            KX = KC + KP - K
            DO 70 J = K + 1, KP - 1
               KX = KX + N - J + 1
               TEMP = AP( KC+J-K )
               AP( KC+J-K ) = AP( KX )
               AP( KX ) = TEMP
   70       CONTINUE
            TEMP = AP( KC )
            AP( KC ) = AP( KPC )
            AP( KPC ) = TEMP
            IF( KSTEP.EQ.2 ) THEN
               TEMP = AP( KC-N+K-1 )
               AP( KC-N+K-1 ) = AP( KC-N+KP-1 )
               AP( KC-N+KP-1 ) = TEMP
            END IF
         END IF
         !
         K = K - KSTEP
         KC = KCNEXT
         GO TO 60
   80    CONTINUE
      END IF
      !
      RETURN
      !
      !      End of DSPTRI
      !
      END
      SUBROUTINE DSPMV ( UPLO, N, ALPHA, AP, X, INCX, BETA, Y, INCY )
      !      .. Scalar Arguments ..
      DOUBLE PRECISION   ALPHA, BETA
      INTEGER            INCX, INCY, N
      CHARACTER*1        UPLO
      !      .. Array Arguments ..
      DOUBLE PRECISION   AP( * ), X( * ), Y( * )
      !      ..
      !
      !   Purpose
      !   =======
      !
      !   DSPMV  performs the matrix-vector operation
      !
      !      y := alpha*A*x + beta*y,
      !
      !   where alpha and beta are scalars, x and y are n element vectors and
      !   A is an n by n symmetric matrix, supplied in packed form.
      !
      !   Parameters
      !   ==========
      !
      !   UPLO   - CHARACTER*1.
      !            On entry, UPLO specifies whether the upper or lower
      !            triangular part of the matrix A is supplied in the packed
      !            array AP as follows:
      !
      !               UPLO = 'U' or 'u'   The upper triangular part of A is
      !                                   supplied in AP.
      !
      !               UPLO = 'L' or 'l'   The lower triangular part of A is
      !                                   supplied in AP.
      !
      !            Unchanged on exit.
      !
      !   N      - INTEGER.
      !            On entry, N specifies the order of the matrix A.
      !            N must be at least zero.
      !            Unchanged on exit.
      !
      !   ALPHA  - DOUBLE PRECISION.
      !            On entry, ALPHA specifies the scalar alpha.
      !            Unchanged on exit.
      !
      !   AP     - DOUBLE PRECISION array of DIMENSION at least
      !            ( ( n*( n + 1 ) )/2 ).
      !            Before entry with UPLO = 'U' or 'u', the array AP must
      !            contain the upper triangular part of the symmetric matrix
      !            packed sequentially, column by column, so that AP( 1 )
      !            contains a( 1, 1 ), AP( 2 ) and AP( 3 ) contain a( 1, 2 )
      !            and a( 2, 2 ) respectively, and so on.
      !            Before entry with UPLO = 'L' or 'l', the array AP must
      !            contain the lower triangular part of the symmetric matrix
      !            packed sequentially, column by column, so that AP( 1 )
      !            contains a( 1, 1 ), AP( 2 ) and AP( 3 ) contain a( 2, 1 )
      !            and a( 3, 1 ) respectively, and so on.
      !            Unchanged on exit.
      !
      !   X      - DOUBLE PRECISION array of dimension at least
      !            ( 1 + ( n - 1 )*abs( INCX ) ).
      !            Before entry, the incremented array X must contain the n
      !            element vector x.
      !            Unchanged on exit.
      !
      !   INCX   - INTEGER.
      !            On entry, INCX specifies the increment for the elements of
      !            X. INCX must not be zero.
      !            Unchanged on exit.
      !
      !   BETA   - DOUBLE PRECISION.
      !            On entry, BETA specifies the scalar beta. When BETA is
      !            supplied as zero then Y need not be set on input.
      !            Unchanged on exit.
      !
      !   Y      - DOUBLE PRECISION array of dimension at least
      !            ( 1 + ( n - 1 )*abs( INCY ) ).
      !            Before entry, the incremented array Y must contain the n
      !            element vector y. On exit, Y is overwritten by the updated
      !            vector y.
      !
      !   INCY   - INTEGER.
      !            On entry, INCY specifies the increment for the elements of
      !            Y. INCY must not be zero.
      !            Unchanged on exit.
      !
      !
      !   Level 2 Blas routine.
      !
      !   -- Written on 22-October-1986.
      !      Jack Dongarra, Argonne National Lab.
      !      Jeremy Du Croz, Nag Central Office.
      !      Sven Hammarling, Nag Central Office.
      !      Richard Hanson, Sandia National Labs.
      !
      !
      !      .. Parameters ..
      DOUBLE PRECISION   ONE         , ZERO
      PARAMETER        ( ONE = 1.0D+0, ZERO = 0.0D+0 )
      !      .. Local Scalars ..
      DOUBLE PRECISION   TEMP1, TEMP2
      INTEGER            I, INFO, IX, IY, J, JX, JY, K, KK, KX, KY
      !      .. External Functions ..
      LOGICAL            LSAME
      EXTERNAL           LSAME
      !      .. External Subroutines ..
      EXTERNAL           XERBLA
      !      ..
      !      .. Executable Statements ..
      !
      !      Test the input parameters.
      !
      INFO = 0
      IF     ( .NOT.LSAME( UPLO, 'U' ).AND.&
               .NOT.LSAME( UPLO, 'L' )      )THEN
         INFO = 1
      ELSE IF( N.LT.0 )THEN
         INFO = 2
      ELSE IF( INCX.EQ.0 )THEN
         INFO = 6
      ELSE IF( INCY.EQ.0 )THEN
         INFO = 9
      END IF
      IF( INFO.NE.0 )THEN
         CALL XERBLA( 'DSPMV ', INFO )
         RETURN
      END IF
      !
      !      Quick return if possible.
      !
      IF( ( N.EQ.0 ).OR.( ( ALPHA.EQ.ZERO ).AND.( BETA.EQ.ONE ) ) )&
         RETURN
      !
      !      Set up the start points in  X  and  Y.
      !
      IF( INCX.GT.0 )THEN
         KX = 1
      ELSE
         KX = 1 - ( N - 1 )*INCX
      END IF
      IF( INCY.GT.0 )THEN
         KY = 1
      ELSE
         KY = 1 - ( N - 1 )*INCY
      END IF
      !
      !      Start the operations. In this version the elements of the array AP
      !      are accessed sequentially with one pass through AP.
      !
      !      First form  y := beta*y.
      !
      IF( BETA.NE.ONE )THEN
         IF( INCY.EQ.1 )THEN
            IF( BETA.EQ.ZERO )THEN
               DO 10, I = 1, N
                  Y( I ) = ZERO
   10          CONTINUE
            ELSE
               DO 20, I = 1, N
                  Y( I ) = BETA*Y( I )
   20          CONTINUE
            END IF
         ELSE
            IY = KY
            IF( BETA.EQ.ZERO )THEN
               DO 30, I = 1, N
                  Y( IY ) = ZERO
                  IY      = IY   + INCY
   30          CONTINUE
            ELSE
               DO 40, I = 1, N
                  Y( IY ) = BETA*Y( IY )
                  IY      = IY           + INCY
   40          CONTINUE
            END IF
         END IF
      END IF
      IF( ALPHA.EQ.ZERO )&
         RETURN
      KK = 1
      IF( LSAME( UPLO, 'U' ) )THEN
         !
         !         Form  y  when AP contains the upper triangle.
         !
         IF( ( INCX.EQ.1 ).AND.( INCY.EQ.1 ) )THEN
            DO 60, J = 1, N
               TEMP1 = ALPHA*X( J )
               TEMP2 = ZERO
               K     = KK
               DO 50, I = 1, J - 1
                  Y( I ) = Y( I ) + TEMP1*AP( K )
                  TEMP2  = TEMP2  + AP( K )*X( I )
                  K      = K      + 1
   50          CONTINUE
               Y( J ) = Y( J ) + TEMP1*AP( KK + J - 1 ) + ALPHA*TEMP2
               KK     = KK     + J
   60       CONTINUE
         ELSE
            JX = KX
            JY = KY
            DO 80, J = 1, N
               TEMP1 = ALPHA*X( JX )
               TEMP2 = ZERO
               IX    = KX
               IY    = KY
               DO 70, K = KK, KK + J - 2
                  Y( IY ) = Y( IY ) + TEMP1*AP( K )
                  TEMP2   = TEMP2   + AP( K )*X( IX )
                  IX      = IX      + INCX
                  IY      = IY      + INCY
   70          CONTINUE
               Y( JY ) = Y( JY ) + TEMP1*AP( KK + J - 1 ) + ALPHA*TEMP2
               JX      = JX      + INCX
               JY      = JY      + INCY
               KK      = KK      + J
   80       CONTINUE
         END IF
      ELSE
         !
         !         Form  y  when AP contains the lower triangle.
         !
         IF( ( INCX.EQ.1 ).AND.( INCY.EQ.1 ) )THEN
            DO 100, J = 1, N
               TEMP1  = ALPHA*X( J )
               TEMP2  = ZERO
               Y( J ) = Y( J )       + TEMP1*AP( KK )
               K      = KK           + 1
               DO 90, I = J + 1, N
                  Y( I ) = Y( I ) + TEMP1*AP( K )
                  TEMP2  = TEMP2  + AP( K )*X( I )
                  K      = K      + 1
   90          CONTINUE
               Y( J ) = Y( J ) + ALPHA*TEMP2
               KK     = KK     + ( N - J + 1 )
  100       CONTINUE
         ELSE
            JX = KX
            JY = KY
            DO 120, J = 1, N
               TEMP1   = ALPHA*X( JX )
               TEMP2   = ZERO
               Y( JY ) = Y( JY )       + TEMP1*AP( KK )
               IX      = JX
               IY      = JY
               DO 110, K = KK + 1, KK + N - J
                  IX      = IX      + INCX
                  IY      = IY      + INCY
                  Y( IY ) = Y( IY ) + TEMP1*AP( K )
                  TEMP2   = TEMP2   + AP( K )*X( IX )
  110          CONTINUE
               Y( JY ) = Y( JY ) + ALPHA*TEMP2
               JX      = JX      + INCX
               JY      = JY      + INCY
               KK      = KK      + ( N - J + 1 )
  120       CONTINUE
         END IF
      END IF
      !
      RETURN
      !
      !      End of DSPMV .
      !
      END
